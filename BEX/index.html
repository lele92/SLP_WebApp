<!DOCTYPE html>

<html lang="en" data-ng-app="SLP_WebApp" ng-strict-di>

<head>
   <meta charset="utf-8">
   <meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1">
   <meta name="description" content="{{app.description}}">
   <meta name="keywords" content="">
   <title data-ng-bind="pageTitle()">BEX</title>
    <!-- Bootstrap styles-->
    <link rel="stylesheet" href="app/css/bootstrap.css">
    <!-- Application styles-->
   <link rel="stylesheet" href="app/css/app.css">
    <!-- Themes-->
    <link rel="stylesheet" href="app/css/theme-f.css" >
    <!-- Custom -->
   <link rel="stylesheet" href="app/css/custom.css">
    <link rel="stylesheet" href="app/css/ngdialog-citations-details.css">
    <!-- Favicon -->
    <link rel="shortcut icon" href="app/img/favicon.ico" type="image/x-icon">
    <link rel="icon" href="app/img/favicon.ico" type="image/x-icon">
</head>

<body data-ng-class="{ 'layout-fixed' : app.layout.isFixed, 'aside-collapsed' : app.layout.isCollapsed, 'layout-boxed' : app.layout.isBoxed }">
   <div data-ui-view="" data-autoscroll="false" data-ng-class="mainViewAnimation" class="wrapper"></div>

   <script src="app/js/base.js"></script>
   <script src="app/js/app.js"></script>
   <script src="app/vendor/canvasjs-1.6.2/canvasjs.min.js"></script>
   <script src="master/js/custom/custom.js"></script>
   <script src="master/js/custom/filters/articlesFilters.js"></script>
   <script src="master/js/custom/controllers/articles-results.js"></script>
   <script src="master/js/custom/controllers/home-search.js"></script>
   <script src="master/js/custom/controllers/filters-offsidebar.js"></script>
   <script src="master/js/custom/controllers/right-navbar.js"></script>
   <script src="master/js/custom/controllers/left-navbar.js"></script>
   <script src="master/js/custom/controllers/orderby-offsidebar.js"></script>
   <script src="master/js/custom/controllers/citations-details-dialog.js"></script>
   <script src="master/js/custom/controllers/bookmarks.js"></script>
   <script src="master/js/custom/controllers/settings.js"></script>
   <script src="master/js/custom/controllers/settings.js"></script>
   <script src="master/js/custom/controllers/about.js"></script>
   <script src="master/js/custom/services/filters-manager.js"></script>
   <script src="master/js/custom/directives/article-item.js"></script>
   <script src="master/js/custom/directives/biblio-item.js"></script>
   <script src="master/js/custom/directives/citing-item.js"></script>
   <script src="master/js/custom/directives/breadcrumb.js"></script>
   <script src="master/js/custom/services/info-articles.js"></script>
   <script src="master/js/custom/services/info-author.js"></script>
   <script src="master/js/custom/services/articles-manager.js"></script>
   <script src="master/js/custom/services/request-articles.js"></script>
   <script src="master/js/custom/services/states-manager.js"></script>
   <script src="master/js/custom/services/response-interceptor.js"></script>
   <script src="master/js/custom/services/bookmarks-manager.js"></script>
   <script src="master/js/custom/services/endpoints-manager.js"></script>
   <script src="master/js/custom/services/ping.js"></script>
   <script src="master/js/custom/services/results-filters-manager.js"></script>


   <!--query: tutti i prefissi-->
   <script id="prefixes" type="text/template">
      PREFIX fabio: <http://purl.org/spar/fabio/>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX frbr: <http://purl.org/vocab/frbr/core#>
      PREFIX co: <http://purl.org/co/>
      PREFIX foaf: <http://xmlns.com/foaf/0.1/>
      PREFIX pro: <http://purl.org/spar/pro/>
      PREFIX dcterms: <http://purl.org/dc/terms/>
      PREFIX c4o: <http://purl.org/spar/c4o/>
      PREFIX cito: <http://purl.org/spar/cito/>
      PREFIX prism: <http://prismstandard.org/namespaces/basic/2.0/>
      PREFIX sro: <http://salt.semanticauthoring.org/ontologies/sro#>
      PREFIX biro: <http://purl.org/spar/biro/>
      PREFIX oa: <http://www.w3.org/ns/oa#>
      PREFIX doco: <http://purl.org/spar/doco/>
   </script>

   <!-- query: info generiche su un articolo -->
   <script id="query_articleInfo" type="text/template">
      select ?title ?doi ?expression ?volumeNumber ?issueNumber ?abstractTxt ?publicationYear ?htmlItem ?authorsList ?globalCountSource ?globalCountValue ?globalCountDate
      where {
         <{{work}}> a fabio:Work;
                  frbr:creator ?authorsList;
                  frbr:realization ?expression.

         ?expression a fabio:Expression;
                  dcterms:title ?title;
                  frbr:embodiment ?man;
                  prism:doi ?doi;
                  fabio:hasPublicationYear ?publicationYear;
                  c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
                                                      c4o:hasGlobalCountSource ?globalCountSource;
                                                      c4o:hasGlobalCountValue ?globalCountValue;
                                                      c4o:hasGlobalCountDate ?globalCountDate].

         ?authorsList a co:List.

         ?man a fabio:Manifestation, fabio:WebPage;
               dcterms:format <http://purl.org/NET/mediatypes/text/html>;
               frbr:exemplar ?htmlItem.

          OPTIONAL {
              ?expression ^frbr:part ?issue.
              ?issue a fabio:JournalIssue;
                    prism:issueIdentifier ?issueNumber;
                    frbr:partOf ?volume.

              ?volume a fabio:JournalVolume;
                    prism:volume ?volumeNumber.
          }

          OPTIONAL {
              ?expression frbr:part [ a sro:Abstract;
              c4o:hasContent ?abstractTxt].
          }
      }
   </script>

   <!-- query: autori di un articolo -->
   <script id="query_articleAuthors" type="text/template">
      select ?givenName ?familyName (concat(?givenName, " ",?familyName) as ?fullName)
      where {
         <{{authorsList}}> a co:List;
                        co:item ?item.

      ?item a co:ListItem;
            co:itemContent ?author.

      ?author a foaf:Person;
            foaf:givenName ?givenName;
            foaf:familyName ?familyName.
      }
   </script>


   <!-- query: numero di citation acts in entrata (raggruppati per colore) -->
   <script id="query_incomingCitationsActsColor" type="text/template">
      select ?color (count(?color) as ?count)
      where {
         ?citAct a cito:CitationAct;
               cito:hasCitingEntity ?citingEntity;
               cito:hasCitedEntity <{{expression}}>;
               cito:hasCitationCharacterization ?color
      }
      group by (?color)
   </script>

   <!-- query: numero di citazioni uniche (cito:cites) in entrata (verso un determinato articolo)-->
   <script id="query_incomingCites" type="text/template">
       select (count(?expr) as ?numCites)
       where {
            ?expr a fabio:Expression;
            cito:cites <{{expression}}>.
       }
   </script>

   <!-- query: numero di InTextReferencePointer "in entrata" e di cites in entrata -->
   <script id="query_incomingCitationsActs" type="text/template">
       select (count(?citAct) as ?numCitActs) (count(?expr) as ?numCites)
       where {
            {
                ?citAct a cito:CitationAct;
                cito:hasCitedEntity <{{expression}}>.
            }
            union
            {
               ?expr a fabio:Expression;
               cito:cites <{{expression}}>.
            }
       }
   </script>

   <!-- query: info sulla citazione (colore) e sugli articoli citati (cited entity) da un paper (citing entity) a partire dai citation acts-->
   <script id="query_biblioInfo" type="text/template">
       select ?color ?citedEntityExpr ?work ?authorsList ?title ?publicationYear ?htmlItem
       where {
            ?citAct a cito:CitationAct;
                    cito:hasCitingEntity <{{expression}}>;
                    cito:hasCitedEntity ?citedEntityExpr;
                    cito:hasCitationCharacterization ?color.

            ?citedEntityExpr a fabio:Expression;
                    dcterms:title ?title;
                    frbr:embodiment ?man;
                    fabio:hasPublicationYear ?publicationYear;
                    frbr:realizationOf ?work.

            ?work a fabio:Work;
                frbr:creator ?authorsList.


           ?authorsList a co:List.

           ?man a fabio:Manifestation, fabio:WebPage;
                    dcterms:format <http://purl.org/NET/mediatypes/text/html>;
                    frbr:exemplar ?htmlItem.
       }
   </script>

   <!-- query: info sugli articoli citati (cito:cites, quindi articoli unici) da un articolo (partendo dall'expression)-->
   <script id="query_citedArticles" type="text/template">
       select ?citedExpression ?articleWork ?articleExpression ?authorsList ?title ?publicationYear ?abstractTxt ?htmlItem ?globalCountValue ?globalCountDate
       where {
            <{{expression}}> cito:cites ?citedExpression.

            ?citedExpression a fabio:Expression;
                            dcterms:title ?title;
                            frbr:embodiment ?man;
                            fabio:hasPublicationYear ?publicationYear;
                            frbr:realizationOf ?articleWork;
                            frbr:part [ a sro:Abstract;
                                            c4o:hasContent ?abstractTxt];
                            c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
                                                                c4o:hasGlobalCountValue ?globalCountValue;
                                                                c4o:hasGlobalCountDate ?globalCountDate].

            ?articleWork a fabio:Work;
                frbr:realization ?articleExpression;
                frbr:creator ?authorsList.

            ?authorsList a co:List.

            ?man a fabio:Manifestation, fabio:WebPage;
                dcterms:format <http://purl.org/NET/mediatypes/text/html>;
                frbr:exemplar ?htmlItem.
       }
   </script>

   <!-- query: quante volte un citing entity cita un cited entity per un certo motivo (colore)-->
   <script id="query_citationActsInfo" type="text/template">
       select ?color (count(?citationAct) as ?numCitActs)
       where {
            ?citationAct a cito:CitationAct;
                        cito:hasCitingEntity <{{artExpression}}>;
                        cito:hasCitedEntity <{{citedExpression}}>;
                        cito:hasCitationCharacterization ?color
       } group by (?color)
   </script>

   <!-- query: quante volte un citing entity cita un cited entity per un certo motivo (colore), senza raggruppamento-->
   <script id="query_citationActsInfoNotGrouped" type="text/template">
       select ?color ?irpTxt ?sentenceTxt
               where {
               ?citationAct a cito:CitationAct;
                   cito:hasCitingEntity <{{artExpression}}>;
                   cito:hasCitedEntity <{{citedExpression}}>;
                   cito:hasCitationCharacterization ?color.

               ?citationAct ^oa:hasBody ?ann.

               ?ann a oa:Annotation;
                   oa:hasTarget ?irp.

               ?irp a c4o:InTextReferencePointer;
                   c4o:hasContent ?irpTxt;
                   c4o:hasContext [ a doco:Sentence;
                                     c4o:hasContent ?sentenceTxt].
       }
   </script>

   <!-- query: info sulle citazioni (citation acts) ad un certo articolo-->
   <script id="query_citationsInfo" type="text/template">
       select ?citingExp ?color ?citingPubYear ?irpTxt ?sentenceTxt
       where {
            ?citAct a cito:CitationAct;
                    cito:hasCitingEntity ?citingExp;
                    cito:hasCitedEntity <{{citedExpression}}>;
                    cito:hasCitationCharacterization ?color.

           ?citAct ^oa:hasBody ?ann.

           ?ann a oa:Annotation;
                    oa:hasTarget ?irp.

           ?irp a c4o:InTextReferencePointer;
                   c4o:hasContent ?irpTxt;
                   c4o:hasContext [ a doco:Sentence;
                                        c4o:hasContent ?sentenceTxt].

            ?citingExp a fabio:Expression;
                   fabio:hasPublicationYear ?citingPubYear.
       }
   </script>

   <!-- query: tutti gli autori -->
   <script id="query_allAuthors" type="text/template">
       select distinct (concat(?givenName, " ",?familyName) as ?fullName)
       where {
            ?person a foaf:Person;
               foaf:givenName ?givenName;
               foaf:familyName ?familyName.
       }
   </script>

   <!-- query: info generiche sugli articoli citanti-->
   <script id="query_citingArticles" type="text/template">
       select ?citingExp ?articleWork ?articleExpression ?title ?authorsList ?abstractTxt ?publicationYear ?htmlItem ?globalCountValue ?globalCountDate
       where {
            ?citingExp a fabio:Expression;
                    cito:cites <{{citedExpression}}>;
                   dcterms:title ?title;
                   frbr:part [ a sro:Abstract;
                                c4o:hasContent ?abstractTxt];
                   frbr:embodiment ?man;
                   fabio:hasPublicationYear ?publicationYear;
                   frbr:realizationOf ?articleWork;
                   c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
                                                       c4o:hasGlobalCountValue ?globalCountValue;
                                                       c4o:hasGlobalCountDate ?globalCountDate].

       ?articleWork a fabio:Work;
                frbr:realization ?articleExpression;
                frbr:creator ?authorsList.

       ?authorsList a co:List.

       ?man a fabio:Manifestation, fabio:WebPage;
                dcterms:format <http://purl.org/NET/mediatypes/text/html>;
                frbr:exemplar ?htmlItem.
       }
   </script>


   <!-- query: info generiche su un articolo (partendo dall'expression) (id:title) -->
   <script id="query_singleArticle" type="text/template">
       select ?title ?doi ?expression ?abstractTxt ?publicationYear ?htmlItem ?authorsList ?globalCountSource ?globalCountValue ?globalCountDate
       where {

                ?expression a fabio:Expression;
               frbr:part [ a sro:Abstract;
                                c4o:hasContent ?abstractTxt];
               dcterms:title "{{title}}";
               frbr:embodiment ?man;
               frbr:realizationOf ?articleWork;
               prism:doi ?doi;
               fabio:hasPublicationYear ?publicationYear;
               c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
                                                   c4o:hasGlobalCountSource ?globalCountSource;
                                                   c4o:hasGlobalCountValue ?globalCountValue;
                                                   c4o:hasGlobalCountDate ?globalCountDate].

           ?articleWork a fabio:Work;
               frbr:creator ?authorsList.

           ?authorsList a co:List.

           ?man a fabio:Manifestation, fabio:WebPage;
               dcterms:format <http://purl.org/NET/mediatypes/text/html>;
               frbr:exemplar ?htmlItem.
       }
   </script>

   <!-- query: info generiche su un articolo (partendo dall'expression) (id: doi)-->
   <script id="query_singleArticleByDoi" type="text/template">
       select ?title ?expression ?abstractTxt ?publicationYear ?htmlItem ?authorsList ?globalCountSource ?globalCountValue ?globalCountDate
       where {

       ?expression a fabio:Expression;
       frbr:part [ a sro:Abstract;
       c4o:hasContent ?abstractTxt];
       dcterms:title ?title;
       frbr:embodiment ?man;
       frbr:realizationOf ?articleWork;
       prism:doi "{{doi}}";
       fabio:hasPublicationYear ?publicationYear;
       c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
       c4o:hasGlobalCountSource ?globalCountSource;
       c4o:hasGlobalCountValue ?globalCountValue;
       c4o:hasGlobalCountDate ?globalCountDate].

       ?articleWork a fabio:Work;
       frbr:creator ?authorsList.

       ?authorsList a co:List.

       ?man a fabio:Manifestation, fabio:WebPage;
       dcterms:format <http://purl.org/NET/mediatypes/text/html>;
       frbr:exemplar ?htmlItem.
       }
   </script>

   <!-- query: tutti gli articoli (work) di un autore -->
   <script id="query_authorArticles" type="text/template">
       select ?work
       where {
            ?author a foaf:Person;
                    foaf:givenName "{{authorGivenName}}";
                    foaf:familyName "{{authorFamilyName}}";
                    pro:holdsRoleInTime [ a pro:RoleInTime;
                                            pro:relatesToDocument ?work ]

            FILTER NOT EXISTS { ?work frbr:realization [ a fabio:JournalEditorial ] }
            FILTER EXISTS {
                ?work frbr:realization [ a fabio:Expression;
                                               frbr:part [ a sro:Abstract;
                                                                c4o:hasContent ?abstractTxt];
                                               dcterms:title ?title;
                                               frbr:embodiment ?man;
                                               prism:doi ?doi;
                                               fabio:hasPublicationYear ?publicationYear;
                                               c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
                                                                                   c4o:hasGlobalCountSource ?globalCountSource;
                                                                                   c4o:hasGlobalCountValue ?globalCountValue;
                                                                                   c4o:hasGlobalCountDate ?globalCountDate]
                                       ]
            }
       }
   </script>

   <!-- query: tutti gli articoli (work) di un autore -->
   <script id="query_FullNameAuthorArticles" type="text/template">
       select ?work
       where {
       ?author a foaf:Person;
       rdfs:label ?label;
       pro:holdsRoleInTime [ a pro:RoleInTime;
       pro:relatesToDocument ?work ]

       FILTER NOT EXISTS { ?work frbr:realization [ a fabio:JournalEditorial ] }
       FILTER EXISTS {
       ?work frbr:realization [ a fabio:Expression;
       frbr:part [ a sro:Abstract;
       c4o:hasContent ?abstractTxt];
       dcterms:title ?title;
       frbr:embodiment ?man;
       prism:doi ?doi;
       fabio:hasPublicationYear ?publicationYear;
       c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
       c4o:hasGlobalCountSource ?globalCountSource;
       c4o:hasGlobalCountValue ?globalCountValue;
       c4o:hasGlobalCountDate ?globalCountDate]
       ]
       }

       FILTER regex(?label, "{{authorFullName}}", "i")
       }
   </script>

   <!-- query: info generiche su articoli con una certa stringa nel titolo -->
   <script id="query_articlesInfo_fromTitle" type="text/template">
       select ?title ?doi ?expression ?volumeNumber ?issueNumber ?abstractTxt ?publicationYear ?htmlItem ?authorsList ?globalCountSource ?globalCountValue ?globalCountDate
       where {

           ?expression a fabio:Expression;
            dcterms:title ?title;
                frbr:part [ a sro:Abstract;
                            c4o:hasContent ?abstractTxt];
           frbr:realizationOf [ a fabio:Work;
                                   frbr:creator ?authorsList;
                                   frbr:realization ?expression];
           frbr:embodiment ?man;
           prism:doi ?doi;
           fabio:hasPublicationYear ?publicationYear;
           c4o:hasGlobalCitationFrequency [ a c4o:GlobalCitationCount;
                                               c4o:hasGlobalCountSource ?globalCountSource;
                                               c4o:hasGlobalCountValue ?globalCountValue;
                                               c4o:hasGlobalCountDate ?globalCountDate].

           ?authorsList a co:List.

           ?man a fabio:Manifestation, fabio:WebPage;
               dcterms:format <http://purl.org/NET/mediatypes/text/html>;
               frbr:exemplar ?htmlItem.

           OPTIONAL {
               ?expression ^frbr:part ?issue.
               ?issue a fabio:JournalIssue;
               prism:issueIdentifier ?issueNumber;
               frbr:partOf ?volume.

               ?volume a fabio:JournalVolume;
               prism:volume ?volumeNumber.
           }

            FILTER regex(?title, "{{title}}", "i")
       }
   </script>

   <!-- query: info generiche su articoli con una certa stringa nel titolo -->
   <script id="query_articlesWork_fromTitle" type="text/template">
       select ?work
       where {
            ?exp a fabio:Expression;
                dcterms:title ?title.

            ?work a fabio:Work;
                frbr:realization ?exp.

            FILTER regex(?title, "{{title}}", "i")
       } LIMIT 50
   </script>

    <script id="" type="text/template">
        select ?citingExp
        where {
            ?irp a c4o:InTextReferencePointer;
                c4o:hasContent ?irpTxt;
                c4o:hasContext [ a doco:Sentence;
                                    frbr:partOf <{{citingEntity}}>;
                                    c4o:hasContent ?sentenceTxt];
                c4o:denotes [ biro:references ?citedEntity].

                ?citedEntity a fabio:Expression.

            OPTIONAL {
                ?irp ^oa:hasTarget ?ann.
                ?ann oa:hasBody ?citAct.
                ?citAct a cito:CitationAct;
                    cito:hasCitedEntity ?citedEntity;
                    cito:hasCitingEntity ?citingExp;
                    cito:hasCitationCharacterization ?color.
            }
        }
    </script>


   <script id="query_articleType" type="text/template">
        select ?type
        where {
            <{{expression}}> a ?type.

            FILTER (?type != fabio:Expression)
        }
   </script>
</body>
</html>



